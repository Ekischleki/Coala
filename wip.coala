collection bool {
    sub and(bool: a, bool: b) {} = not bool::nand(a, b)
    sub nand(bool: a, bool: b) {} = or(not a, not b)
    sub nor(bool: a, bool: b) {} = not or(a, b)
    sub xor(bool: a, bool: b) {
        let not_both_true = bool::nand(a, b)
        let either_true = or(a, b)
    } = bool::and(not_both_true, either_true)
    sub xnor(bool: a, bool: b) {
        let both_true = bool::and(a, b)
        let both_false = bool::nor(a, b)
    } = or(both_true, both_false)
    sub eq(bool: a, bool: b) {} = bool::xnor(a, b)
}
collection bit {
    # Selects a if select is true, selects b otherwise
    sub select(bool: a, bool: b, bool: select) {
        let select_a = bool::and(a, select)
        let select_b = bool::and(b, not select)
    } = or (select_a, select_b)

    sub full_adder(bool: a, bool: b, bool: c) {
        let a_xor_b = bool::xor(a, b)
        let data = bool::xor(c, a_xor_b)
        let carry = or(bool::and(a, b), bool::and(c, a_xor_b))
    } = ArithmeticResult {
        carry: carry,
        data: data
    }
    sub half_adder(bool: a, bool: b) {
        let carry = bool::and(a, b)
        let data = bool::xor(a, b)
    } = ArithmeticResult {
            data: data, 
            carry: carry
        }
}
collection int {
    sub zero(sup: int_length) {
        let data = [false; int_length]
    } = Int {
        data: data,
        int_length: int_length
    }
    sub from_super(sup: int, sup: int_length) {
        let data = [false; int_length]
        let i = 0
        for (i in int_length) {
            if (super::eq(super::mod(int, 2), 1)) {
                data[i] = true
            }
            int = super::div(int, 2)
        }
    } = Int {
        data: data,
        int_length: int_length
    }
    sub add(Int: a, Int: b) {
        force super::eq(a.int_length, b.int_length) => true

        let carry = false
        let data = [false; a.int_length]
        for (i in a.int_length) {
            let a_i = a.data[i]
            let b_i = b.data[i]
            let res = bit::full_adder(a_i, b_i, carry)
            data[i] = res.data
            carry = res.carry
        }
    } = Int {
        data: data,
        int_length: a.int_length
    }
    sub eq(Int: a, Int: b) {
        force super::eq(a.int_length, b.int_length) => true

        let eq = true
        for (i in a.int_length) {
            let a_i = a.data[i]
            let b_i = b.data[i]
            if (not bool::eq(a_i, b_i)) {
                eq = false
            }
        }
    } = eq
    

}

composite Int {
    array: data,
    super_i: int_length
}
collection assert {
    sub eq(bool: a, bool: b) {
        force bool::eq(a, b) => true
    }
    sub neq(bool: a, bool: b) {
        force bool::eq(a, b) => false
    }
    sub is_true(bool: a) {
        force a => true
    }
    sub is_false(bool: a) {
        force a => false
    }
}

composite ArithmeticResult {
    bool: data,
    bool: carry
}

#Defines as super value which can only be used as a constant to expand loop macros.
super INT_LENGTH = 16

problem {

    sub test() {
        let a = int::from_super(50, 8)
        let b = int::from_super(19, 8)
        let c = int::add(a, b)
        let res = int::from_super(69, 8)
        force int::eq(c, res) => true
    }

}


solution {
    # (110 + 010 = 101): 3 + 2 = 5
    test()
}


